PREFIX oc: <http://mu.semte.ch/vocabularies/ext/oc/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

DELETE {
    GRAPH ?g {
        ?meeting prov:startedAtTime ?date .
    }
}
INSERT {
    GRAPH ?g {
        ?meeting prov:startedAtTime ?datetime .
    }
}
WHERE {
    GRAPH ?g {
        ?meeting a oc:Meeting .
        ?meeting prov:startedAtTime ?date .
        FILTER ( datatype(?date) = xsd:date )
        FILTER ( regex(str(?date), '\\d{4}-\\d\\d-\\d\\d') )
        BIND (xsd:dateTime(CONCAT(str(?date), 'T08:00:00+02:00')) AS ?datetime)
    }
    VALUES ?g {
        <http://mu.semte.ch/graphs/organizations/kanselarij>
        <http://mu.semte.ch/graphs/organizations/minister>
        <http://mu.semte.ch/graphs/organizations/kabinet>
        <http://mu.semte.ch/graphs/organizations/administratie>
    }
}

